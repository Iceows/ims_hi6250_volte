/*
 * This file is part of HwIms
 * Copyright (C) 2024 Raphael Mounier
 *     This program is free software: you can redistribute it and/or modify
 *     it under the terms of the GNU General Public License as published by
 *     the Free Software Foundation, either version 3 of the License, or
 *     (at your option) any later version.
 *
 *     This program is distributed in the hope that it will be useful,
 *     but WITHOUT ANY WARRANTY; without even the implied warranty of
 *     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *     GNU General Public License for more details.
 *
 *     You should have received a copy of the GNU General Public License
 *     along with this program.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id 'com.gladed.androidgitversion' version '0.4.9'
}

apply plugin: 'com.android.application'

android {
    def getGitHash = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    signingConfigs {
        config {
            keyAlias 'android'
            keyPassword 'android'
            storeFile file('platform.jks')
            storePassword 'android'
        }
    }
    compileSdk 28
    defaultConfig {
        applicationId "com.huawei.ims"
        minSdk 28
        targetSdk 28
        versionCode 1
        versionName androidGitVersion.name()
        signingConfig signingConfigs.config
        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
        aidl true
    }

    sourceSets { main { jni.srcDirs = ['src/main/jniLibs'] } }
}

dependencies {
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'kxml2:kxml2:2.3.0'

    implementation files('.\\libs\\vendor.huawei.hardware.radio-java.jar','.\\libs\\com.hisi.mapcon-java.jar')
    compileOnly  files('.\\sdk\\framework.jar','.\\sdk\\ims-common.jar','.\\sdk\\telephony-common.jar')
}
repositories {
    mavenCentral()
}
